#!/usr/bin/perl
## ----------------------------------------------------------------------------
# cil is a Command line Issue List
# Copyright (C) 2008 Andrew Chilton
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
## ----------------------------------------------------------------------------

use strict;
use warnings;
use CIL::Issue;
use CIL::Comment;
use Term::CallEditor;
use File::Touch;
use File::Glob ':glob';
use File::Basename;

my $COMMANDS = {
    init    => 1,
    list    => 1,
    summary => 1,
    add     => 1,
    show    => 1,
    edit    => 1,
    comment => 1,
};

my $gan = $ENV{GIT_AUTHOR_NAME} || 'Your Name';
my $gae = $ENV{GIT_AUTHOR_EMAIL} || 'you@example.org';

my $new_issue_text = <<"EOF";
[Issue]
Summary     =
Status      =New
CreatedBy   =$gan <$gae>
Labels      =
Description = <<END_OF_DESCRIPTION

END_OF_DESCRIPTION
EOF

my $add_comment_text = <<"EOF";
[Comment]
CreatedBy   = $gan <$gae>
Description = <<END_OF_DESCRIPTION

END_OF_DESCRIPTION
EOF

## ----------------------------------------------------------------------------

{
    my ($command) = shift;
    unless ( defined $command and exists $COMMANDS->{$command} ) {
        usage();
        exit 2;
    }

    if ( $command eq 'init' ) {
        my ($path) = @ARGV;
        $path ||= '.';
        init($path);
    }
    elsif ( $command eq 'list' ) {
        list();

    }
    elsif ( $command eq 'summary' ) {
        summary();

    }
    elsif ( $command eq 'show' ) {
        my ($issue_name) = @ARGV;
        show($issue_name);

    }
    elsif ( $command eq 'add' ) {
        add();
    }
    elsif ( $command eq 'edit' ) {
        my ($issue_name) = @ARGV;
        edit($issue_name);
    }
    elsif ( $command eq 'comment' ) {
        my ($issue_name) = @ARGV;
        comment($issue_name);
    }

    else {
        fatal("program error when trying command '$command'");
    }

}

## ----------------------------------------------------------------------------
# commands

sub init {
    my ($path) = @_;

    # error if $path doesn't exist
    unless ( -d $path ) {
        fatal("path '$path' doesn't exist");
    }

    # error if issues/ already exists
    my $issues_dir = "$path/issues";
    if ( -d $issues_dir ) {
        fatal("issues directory '$issues_dir' already exists, not initialising issues");
    }

    # error if .cil already exists
    my $config = "$path/.cil";
    if ( -f $config ) {
        fatal("config file '$config' already exists, not initialising issues");
    }

    # try to create the issues/ dir
    unless ( mkdir $issues_dir ) {
        fatal("Couldn't create '$issues_dir' directory: $!");
    }

    # create a .cil file here also
    unless ( touch $config ) {
        rmdir $issues_dir;
        fatal("couldn't create a '$config' file");
    }

    # $path/issues/ and $path/.cil create correctly
    msg("initialised empty issue list inside '$path/'");
}

sub list {
    check_paths();

    # find all the issues
    my $issues = get_all_issues();
    if ( @$issues ) {
        separator();
        foreach my $issue ( @$issues ) {
            display_issue_short($issue);
        }
        separator();
    }
    else {
        msg('no issues found');
    }
}

sub summary {
    check_paths();

    # find all the issues
    my $issues = get_all_issues();
    separator();
    title('Summary');
    foreach my $issue ( @$issues ) {
        display_issue_summary($issue);
    }
    separator();
}

sub show {
    my ($issue_name) = @_;

    # firstly, read the issue in
    my $issue = CIL::Issue->new_load_issue($issue_name);
    unless ( defined $issue ) {
        fatal("Couldn't load issue '$issue'");
    }
    display_issue_full( $issue );
}

sub add {
    my ($issue_name) = @_;

    # read in the new issue text
    my $fh = solicit( $new_issue_text );

    my $issue;
    eval {
        $issue = CIL::Issue->new_parse_issue( $fh );
    };
    if ( $@ ) {
        fatal("couldn't parse issue: $@");
    }
    unless ( defined $issue ) {
        fatal("couldn't parse issue (program error)");
    }

    $issue->inserted;
    $issue->set_no_update( 'Name', $issue->Inserted );
    $issue->save();
    display_issue_full( $issue );
}

sub edit {
    my ($issue_name) = @_;

    my $issue = CIL::Issue->new_load_issue($issue_name);
    unless ( defined $issue ) {
        fatal("Couldn't load issue '$issue'");
    }

    my $summary     = $issue->Summary;
    my $status      = $issue->Status;
    my $created_by  = $issue->CreatedBy;
    my $labels      = $issue->Labels;
    my $description = $issue->Description;

    # create the ini file, then edit it
    my $edit_issue_text = <<"EOF";
[Issue]

Summary     = $summary
Status      = $status
CreatedBy   = $created_by
Labels      = $labels
Description = <<END_OF_DESCRIPTION
$description
END_OF_DESCRIPTION
EOF

    # read in the new issue text
    my $fh = solicit( $edit_issue_text );

    my $issue_edited;
    eval {
        $issue_edited = CIL::Issue->new_parse_issue( $fh );
    };
    if ( $@ ) {
        fatal("couldn't parse issue: $@");
    }
    unless ( defined $issue_edited ) {
        fatal("couldn't create issue (program error)");
    }

    # for the fields we're interested in, set them
    foreach my $field ( qw(Summary Status CreatedBy Labels Description) ) {
        $issue->$field( $issue_edited->$field() );
    }
    if ( $issue->changed ) {
        $issue->save();
    }
    else {
        msg('Issue not changed');
    }
}

sub comment {
    my ($issue_name) = @_;

    my $issue = CIL::Issue->new_load_issue($issue_name);
    unless ( defined $issue ) {
        fatal("Couldn't load issue '$issue'");
    }

    # read in the new issue text
    my $fh = solicit( $add_comment_text );

    my $cfg;
    eval {
        $cfg = Config::IniFiles->new( -file => $fh );
    };
    if ( $@ ) {
        fatal("couldn't parse comment: $@");
    }

    unless ( defined $cfg ) {
        fatal("not a valid inifile");
    }

    my $comment = CIL::Comment->new();
    foreach my $field ( qw(CreatedBy Description) ) {
        # modify the data directly, otherwise Updated will kick in
        my $value = $cfg->val( 'Comment', $field );
        next unless defined $value;

        $value =~ s/^\s*//;
        $value =~ s/\s*$//;
        $comment->set_no_update($field, $value);
    }

    # tell the comment when it was inserted
    $comment->inserted;

    # add the comment to the issue, update it's timestamp and save it out
    $issue->add_comment( $comment );
    $issue->save();
}

## ----------------------------------------------------------------------------

sub check_paths {
    # make sure an issue directory is available
    unless ( -d 'issues' ) {
        fatal("couldn't find 'issues' directory");
    }
}

sub get_all_issues {
    my @issues;
    my @filenames = <issues/*.yaml>;
    foreach my $filename ( sort @filenames ) {
        push @issues, CIL::Issue->new_load_issue( basename($filename, '.yaml') );
    }
    @issues = sort { $a->Inserted cmp $b->Inserted } @issues;
    return \@issues;
}

## ----------------------------------------------------------------------------
# input/output

sub display_issue_summary {
    my ($issue) = @_;

    my $msg = $issue->Name;
    $msg .= "\t";
    $msg .= $issue->Status;
    $msg .= "\t";
    $msg .= $issue->CreatedBy;
    $msg .= "\t";
    $msg .= $issue->Summary;

    msg($msg);
}

sub display_issue_short {
    my ($issue) = @_;

    title( "Issue " . $issue->Name );
    field( 'Summary', $issue->Summary() );
    field( 'Name', $issue->Name() );
    field( 'CreatedBy', $issue->CreatedBy() );
    field( 'Inserted', $issue->Inserted() );
    field( 'Status', $issue->Status() );
    field( 'Labels', $issue->Labels() );
}

sub display_issue_full {
    my ($issue) = @_;

    separator();
    title('Details');
    field( 'Summary', $issue->Summary() );
    field( 'Name', $issue->Name() );
    field( 'Status', $issue->Status() );
    field( 'CreatedBy', $issue->CreatedBy() );
    field( 'Inserted', $issue->Inserted() );
    field( 'Updated', $issue->Inserted() );
    field( 'Labels', $issue->Labels() );
    separator();
    text('Description', $issue->Description());
    my $comments = $issue->comments();
    foreach my $comment ( @$comments ) {
        separator();
        title('Comment');
        field( 'CreatedBy', $comment->CreatedBy() );
        field( 'Inserted', $comment->Inserted() );
        field( 'Updated', $comment->Inserted() );
        text('Description', $comment->Description());
    }
    separator();
}

sub msg {
    print ( defined $_[0] ? $_[0] : '' );
    print "\n";
}

sub separator {
    msg('=' x 79);
}

sub title {
    my ($title) = @_;
    my $msg = "--- $title ";
    $msg .= '-' x (74 - length($title));
    msg($msg);
}

sub field {
    my ($field, $value) = @_;
    my $msg = "$field";
    $msg .= " " x (12 - length($field));
    msg("$msg: " . (defined $value ? $value : '') );
}

sub text {
    my ($field, $value) = @_;
    title($field);
    msg "";
    msg($value);
    msg "";
}

sub err {
    print STDERR ( defined $_[0] ? $_[0] : '' );
    print STDERR "\n";
}

sub fatal {
    my ($msg) = @_;
    chomp $msg;
    print STDERR $msg, "\n";
    exit 2;
}

## ----------------------------------------------------------------------------
# program info

sub usage {
   print <<"END_USAGE";
Usage: $0 <command> [options]

Commands:
   init    <path>
   add
   list
   show    <issue>
   edit    <issue>

See <http://kapiti.geek.nz/software/cil.html> for further information.
Report bugs to <andychilton -at- gmail -dot- com>.
END_USAGE
}

## ----------------------------------------------------------------------------
